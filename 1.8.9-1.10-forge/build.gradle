buildscript() {
    repositories() {
        mavenCentral()
        gradlePluginPortal()
        maven() {
            url = 'https://maven.minecraftforge.net/'
        }
        maven() {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies() {
        classpath('net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT')
        classpath('com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:4.0.4')
        classpath('org.spongepowered:mixingradle:0.6-SNAPSHOT')
    }
}
apply('plugin': 'net.minecraftforge.gradle.forge')
apply('plugin': 'com.github.johnrengelman.shadow')
apply('plugin': 'org.spongepowered.mixin')

version = '1.4.0'
group= "cn.flowerinsnow.greatscrollabletooltips"
archivesBaseName = "great-scrollable-tooltips"

minecraft() {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'

    mappings = 'stable_20'
    makeObfSourceJar = false
}

repositories() {
    mavenCentral()

    maven() {
        url = 'https://maven.pkg.github.com/flowerinsnowdh/GreatScrollableTooltips'
        credentials() {
            username = "${System.getenv('GITHUB_USERNAME')}"
            password = "${System.getenv('GITHUB_TOKEN')}"
        }
    }

    maven() {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations() {
    shade
    compile.extendsFrom(shade)
}

dependencies() {
    compile("cn.flowerinsnow.greatscrollabletooltips:common:${version_common_module}") {
        exclude('module': 'core')
        exclude('module': 'toml')
    }
    shade("com.electronwill.night-config:toml:${version_night_config}")
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude('module': 'guava')
        exclude('module': 'commons-io')
        exclude('module': 'gson')
    }
}

processResources() {
    LinkedHashMap<String, ?> props = [
            'version' : project.version,
            'mcversion' : project.minecraft.version
    ]

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include('mcmod.info')
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude('mcmod.info')
    }
}

tasks.withType(JavaCompile).configureEach() {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

shadowJar() {
    manifest() {
        attributes([
                'TweakClass': 'cn.flowerinsnow.greatscrollabletooltips.tweaker.GreatScrollableTooltipsTweaker'
        ])
    }

//    configurations = [
//            project.configurations.shade
//    ]

    dependencies() {
        include(dependency("cn.flowerinsnow.greatscrollabletooltips:common:${version_common_module}"))
        include(dependency("com.electronwill.night-config:core:${version_night_config}"))
        include(dependency("com.electronwill.night-config:toml:${version_night_config}"))
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
    }
    exclude('LICENSE.txt')
    exclude('dummyThing')

    ArrayList<String> packagesToRelocate = [
            'com.electronwill.nightconfig.core',
            'com.electronwill.nightconfig.toml'
    ]

    packagesToRelocate.forEach() {
        relocate(it, "cn.flowerinsnow.greatscrollabletooltips.shaded.$it")
    }

    classifier = ''
}

reobf() {
    shadowJar() {
    }
}

mixin() {
    add(sourceSets.main, "great-scrollable-tooltips-refmap.json")
}
